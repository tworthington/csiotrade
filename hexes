wordlist: hexes

hexes first get-current definitions

\ ---------------------

\ b has same y coordingate as a
: level ( bx ax ay by -- d)
    2drop
    diff
;

: samecol? ( bx ax -- flag)
    =
;

: just-y ( bx ax ay by -- d)
    2swap 2drop diff
;

\ Calculate the expected dy, with a mod (0 or 1) based on
\ whether we're going NE or SE and whether starting on
\ an odd row or not
: expectedd ( bx ax mod -- e )
    -rot 
    diff
    + 2/
;

\ The distance is equal to the difference in x, with
\ a modifier based on whether the dy is outside the
\ expected range
: calc ( bx ax ay by adjust  -- d )
    >r 
    2over r> expectedd  ( bx ax ay by e )
    -rot diff ( bx ax e dy )
    2dup < if swap -  else  2drop 0  then  ( bx ax boost )
    -rot diff + 
;

: oddcol ( bx ax ay by -- d)
    2dup >  negate calc
;

: evencol
    2dup >  1+ calc
;

\ ax ay is the start point and bx by is no further west.
: (hexdis) ( bx by  ax ay -- d)
    rot   ( bx ax ay by )
    2dup = if   level    else
        2over = if just-y else 
        2 pick odd?  if   oddcol  else  evencol
    then then then
;
    
: xy>hex# ( x y -- n )
    swap 100 * +
;

: hex#>xy ( n -- x y )
    dup 100 / swap 100 mod ;


: hexdis ( hex1 hex2 -- d)
    hex#>xy rot hex#>xy ( x2 y2 x1 y1)
    over 4 pick ( x2 y2 x1 y1 x1 x2)
    > if 2swap then
    (hexdis)
;


: rhexdis ( hex1 hex2 -- d)
    hex#>xy rot hex#>xy ( x2 y2 x1 y1)
    rot ( x2 x1 y1 y2)
    diff ( x2 x2 yd)
    -rot diff +
;

previous set-current