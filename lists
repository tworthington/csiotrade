wordlist: lists
lists first

get-current definitions

: .list
    dup times i 1+ pick . iterate
;

\ new list
: list   0  ;

\ join lists
: ll+ ( x1 x2..xn n y1 y2..ym m -- x1..xn+m n+m)
    dup >r
    roll r> +
;

\ add item to list
: l+ ( x1..xn n xn+1 -- x1..xn+1 n+1)
    swap 1+
;

: index ( x1..xn n item -- x1..xn n idx|0)
    >r
    dup begin ( xn n n |r: item)
        dup while
            dup 1+ pick r@ = if rdrop exit then
            1-
    repeat
    rdrop
;

: lpop ( xn n -- xn-1 n-1 xn)
    dup if
        1- swap
    else
        abort" lpop on empty list"
    then
;

\ remove item, if it exists
: l- ( x1..xn n i -- x1..x? n-1)
    index ?dup if roll drop 1- then
;

\ like l- but remove all, not just first
: l--
    begin index ?dup while roll drop 1- repeat
;

: ldup dup>r dup times i pick iterate r> ;

: foreach ( list n xt -- )
    swap times  tuck execute  iterate drop ;

: ldrop  drops ;

: clear depth drops ;

: l, ( x1 x2 ... xn n -- )
    dup>r times i pick , iterate r> drops ;

: ,X ( x n -- )
    dup >r dups
    r> 1+ l,
;

: join ( [list1] l1 [list2] l2 -- [list3] l3)
    dup 1+ roll +
;

export{

: .s depth .list drop  ;

}export

previous
set-current
